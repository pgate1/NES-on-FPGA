
/*
	Mapper 000
		PRG-ROM 16KB or 32KB
		CHR-ROM 8KB
*/

circuit Mapper000
{
	// PRG-ROM
	input ROM_SELn;
	instrin prg_read; //, prg_write;
	input prg_A<15>; //, prg_Din<8>;
	output prg_Dout<8>;
	output IRQn;

	// CHR-ROM
	instrin chr_read, chr_write; // RDn, WEn
	input chr_A<14>, chr_Din<8>; // chr_A13n
	output chr_Dout<8>;
	output VRAM_CSn, VRAM_A10;

//	instrin Phi; // 1.789MHz
	output usound<11>, ssound<14>;

	input n16kRomBanks<6>, n8kVRomBanks<6>, fMirroringType;

	// 外部RAMへのアクセス
	instrout prg_rom_read(prg_rom_adrs);
	output prg_rom_adrs<15>; // PRG_ROM 32KBまで
	input prg_rom_rdata<8>;

	instrout chr_ram_read(chr_ram_adrs);
	output chr_ram_adrs<13>; // CHR_ROM 8KB
	output chr_ram_wdata<8>; // add
	instrout chr_ram_write(chr_ram_adrs, chr_ram_wdata); // add
	input chr_ram_rdata<8>;

	par{

		prg_Dout = prg_rom_rdata;

		VRAM_CSn = ^chr_A<13>;

		if(fMirroringType){
			// マリオなどの水平スクロールタイプ
			VRAM_A10 = chr_A<10>;
		}
		else{
			// レッキングクルーなどの垂直スクロールタイプ
			VRAM_A10 = chr_A<11>;
		}

		chr_Dout = chr_ram_rdata;

		IRQn = 0b1;

		usound = // <11>
		   0b00000000000;

		ssound = // <14s>
		   0b00000000000000;

	}

	instruct prg_read par{
		if(ROM_SELn==0b0) switch(n16kRomBanks<1>){
			case 0b0: prg_rom_read(0b0 || prg_A<13:0>);
			case 0b1: prg_rom_read(prg_A);
		}
	}

	instruct chr_read par{
		if(chr_A<13>==0b0){
			chr_ram_read(chr_A<12:0>);
		}
	}

	instruct chr_write if(n8kVRomBanks==0){ // add
		if(chr_A<13>==0b0){
			chr_ram_write(chr_A<12:0>, chr_Din);
		}
	}

}
