
/*
	Mapper 002
	PRGマッパー
		PRG-ROM 16KB × 8 or 16
		CHR-RAM 8KB
*/

circuit Mapper002
{
	// PRG-ROM
	input ROM_SELn;
	instrin prg_read, prg_write;
	input prg_A<15>, prg_Din<8>;
	output prg_Dout<8>;
	output IRQn;

	// CHR-ROM
	instrin chr_read, chr_write; // RDn, WEn
	input chr_A<14>, chr_Din<8>; // chr_A13n
	output chr_Dout<8>;
	output VRAM_CSn, VRAM_A10;

	instrin Phi; // 1.789MHz
	output usound<11>, ssound<14>;

	instrin init;

	// 外部RAMへのアクセス
	instrout prg_rom_read(prg_rom_adrs);
	output prg_rom_adrs<18>; // PRG_ROM 256KBまで
	input prg_rom_rdata<8>;

	instrout chr_ram_read(chr_ram_adrs);
	output chr_ram_adrs<13>; // CHR_RAM 8KB
	output chr_ram_wdata<8>;
	instrout chr_ram_write(chr_ram_adrs, chr_ram_wdata);
	input chr_ram_rdata<8>;

	input n16kRomBanks<6>, fMirroringType;
	reg prg_bank<4>;

	par{

		prg_Dout = prg_rom_rdata;

		// A<13>==1のときVRAM
		VRAM_CSn = ^chr_A<13>;

		if(fMirroringType){
			// 水平スクロールタイプ
			VRAM_A10 = chr_A<10>;
		}
		else{
			// 垂直スクロールタイプ
			VRAM_A10 = chr_A<11>;
		}

		chr_Dout = chr_ram_rdata;

		IRQn = 0b1;

		usound = // <11>
		   0b00000000000;

		ssound = // <14s>
		   0b00000000000000;

	}

	instruct init par{
		prg_bank := 0;
	}

	instruct prg_read par{
		if(ROM_SELn==0b0){
			if(prg_A<14>==0b0) prg_rom_read(prg_bank || prg_A<13:0>);
			else switch(n16kRomBanks<4>){
				case 0b0: prg_rom_read(0b0111 || prg_A<13:0>); //  8 banks
				case 0b1: prg_rom_read(0b1111 || prg_A<13:0>); // 16 banks
			}
		}
	}

	instruct prg_write par{
		if(ROM_SELn==0b0){
			prg_bank := prg_Din<3:0>;
		}
	}

	instruct chr_read par{
		if(chr_A<13>==0b0){
			chr_ram_read(chr_A<12:0>);
		}
	}

	instruct chr_write par{
		if(chr_A<13>==0b0){
			chr_ram_write(chr_A<12:0>, chr_Din);
		}
	}

}
